/* SCEI CONFIDENTIAL
 "PlayStation 2" Programmer Tool Runtime Library  Release 2.0
 */
/* 
 *              Emotion Engine Library Sample Program
 *
 *                       - feather aa1 sample  -
 *
 *                         Version 0.1.0
 *                           Shift-JIS
 *
 *      Copyright (C) 1998-2000 Sony Computer Entertainment Inc.
 *                        All Rights Reserved.
 *
 *                     Name : work.vsm
 *
 *       Version        Date            Design      Log
 *  --------------------------------------------------------------------
 *      0.1.0           Oct,07,1999     sasaki    modified from feather
 *      0.2.0           Jul,11,2000     sasaki    
 */

; ********************************************************************************************************
;	VU-MEM	0-120	parameters
;		120-256	packet1
;		260-396	packet2
;		398-	output buffer
; ********************************************************************************************************
;	VI00	0
;	VI01	work
;	VI02	work
;	VI03	work
;	VI04	negative reference
;	VI05	GIF tag pointer (XTOP)
;	VI06	RGBA read pointer
;	VI07	XYZ read pointer
;	VI08	NORMAL read pointer
;	VI09	buffer flag
;	VI10	normalized UV pointer
;	VI11	NLOOP
;	VI12	store pointer
;	VI13	negative flag
;	VI14	stack pointer
;	VI15	link register
; ********************************************************************************************************
;	VF00	0 0 0 1
;	VF01	vertex position
;	VF02	vertex normal
;	VF03	texture UV
;	VF04	RGBA
;	VF05	vertex position in the screen coordinate
;	VF06	vertex normal in the screen coordinate
;	VF07	PRMODE command
;	VF08	distance from eye to vertex
;	VF09	coef to dismiss the triangle
;	VF10-13	World/Local matrix
;	VF14	output RGBA
;	VF15	output XYZ*Q
;	VF16	ambient color
;	VF17	output normal
;	VF18	output UV*Q
;	VF19	max color
;	VF20-23	Prim/Local matrix
;	VF24-26 Light/Local matrix
;	VF27-30	Light/Color matrix
;	VF28	VF05*VF06(override)
;	VF29	sum of VF05*VF06(override)
;	VF31	GIF Tag
; ********************************************************************************************************
;	RotTransPers+Lighting
;
main: 	NOP   	 			NOP   
	SUB.xyzw VF01, VF00, VF00	IADDIU VI15, VI00, 0
	SUB.xyzw VF02, VF00, VF00	IADDIU VI14, VI00, 0
	SUB.xyzw VF03, VF00, VF00	IADDIU VI13, VI00, 0
	SUB.xyzw VF04, VF00, VF00	IADDIU VI12, VI00, 0
	SUB.xyzw VF05, VF00, VF00	IADDIU VI11, VI00, 0
	SUB.xyzw VF06, VF00, VF00	IADDIU VI10, VI00, 0
	SUB.xyzw VF07, VF00, VF00	IADDIU VI09, VI00, 0
	SUB.xyzw VF08, VF00, VF00	IADDIU VI08, VI00, 0
	SUB.xyzw VF09, VF00, VF00	IADDIU VI07, VI00, 0
	SUB.xyzw VF11, VF00, VF00	IADDIU VI06, VI00, 0
	SUB.xyzw VF12, VF00, VF00	IADDIU VI05, VI00, 0
	SUB.xyzw VF13, VF00, VF00	IADDIU VI04, VI00, 0
	SUB.xyzw VF14, VF00, VF00	IADDIU VI03, VI00, 0
	SUB.xyzw VF15, VF00, VF00	IADDIU VI02, VI00, 0
	SUB.xyzw VF16, VF00, VF00	IADDIU VI01, VI00, 0
	SUB.xyzw VF17, VF00, VF00	NOP
	SUB.xyzw VF18, VF00, VF00	NOP
	SUB.xyzw VF19, VF00, VF00	NOP
	SUB.xyzw VF10, VF00, VF00	NOP
	SUB.xyzw VF20, VF00, VF00	NOP
	SUB.xyzw VF21, VF00, VF00	NOP
	SUB.xyzw VF22, VF00, VF00	NOP
	SUB.xyzw VF23, VF00, VF00	NOP
	SUB.xyzw VF24, VF00, VF00	NOP
	SUB.xyzw VF25, VF00, VF00	NOP
	SUB.xyzw VF26, VF00, VF00	NOP
	SUB.xyzw VF27, VF00, VF00	NOP
	SUB.xyzw VF28, VF00, VF00	NOP
	SUB.xyzw VF29, VF00, VF00	NOP
	SUB.xyzw VF30, VF00, VF00	NOP
	SUB.xyzw VF31, VF00, VF00	NOP
	NOP   	 			IADDIU VI01, VI00, 1
	NOP   	 			IAND VI09, VI09, VI01		;VI09=LSB(VI09)
;
	NOP   	 			IADDIU VI14, VI00, 1024
	NOP   	 			IADDIU VI01, VI00, 8
	NOP   	 			IADDIU VI02, VI00, 22
	NOP   	 			IADDIU VI03, VI00, 12		;[Light/LOCAL]
	NOP   	 			NOP   
	NOP   	 			BAL VI15, MulMatrix 
	NOP   	 			NOP   
;
	NOP				  LQ.xyzw VF10, 22(VI00)		;[VF10,VF11,VF12,VF13]=[Screen/World]
	NOP				  LQ.xyzw VF11, 23(VI00)
	NOP				  LQ.xyzw VF12, 24(VI00)
	NOP				  LQ.xyzw VF13, 25(VI00)
;
	NOP   	 			LQ.xyzw VF20, 30(VI00) 		;[VF20,VF21,VF22,VF23]=[PRIM/LOCAL]
	NOP   	 			LQ.xyzw VF21, 31(VI00) 
	NOP   	 			LQ.xyzw VF22, 32(VI00) 
	NOP   	 			LQ.xyzw VF23, 33(VI00) 
;
	NOP   	 			LQ.xyzw VF24, 12(VI00) 		;[VF24,VF25,VF26]=[Light/LOCAL]
	NOP   	 			LQ.xyzw VF25, 13(VI00) 
	NOP   	 			LQ.xyzw VF26, 14(VI00) 
;
	NOP   	 			LQ.xyzw VF27, 4(VI00) 		;[VF27,VF28,VF29,VF30]=[Light Color]
	NOP   	 			LQ.xyzw VF28, 5(VI00) 
	NOP   	 			LQ.xyzw VF29, 6(VI00) 
	NOP   	 			LQ.xyzw VF30, 7(VI00) 
;
	NOP				LQ.xyzw VF19, 17(VI00)		;max color 255
;
START_P:NOP   	 			XTOP VI05  
	NOP   	 			NOP   
	NOP   	 			NOP   
	NOP   	 			NOP   
	NOP   	 			ILW.x VI02, 0(VI05) 		;VI05->GIFtag
	NOP   	 			IADDIU VI03, VI00, 0x7fff
	NOP   	 			NOP   
	NOP   	 			NOP   
	NOP   	 			IAND VI11, VI02, VI03		;VI11=NLOOP
;
	  SUB.xyzw VF07, VF00, VF00   	IADDIU VI06, VI05, 1		;VI06->RGBA
	  SUB.xyzw VF09, VF00, VF00  	IADD VI07, VI06, VI11		;VI07->XYZ
	  FTOI15.w VF06w, VF00w		IADD VI08, VI07, VI11		;VI08->NxNyNz
	NOP   	 			IADD VI10, VI08, VI11		;VI10->normalized UV
	NOP   	 			IBNE VI09, VI00, START
	NOP   	 			IADDIU VI12, VI12, 6
	NOP   	 			ILW.w VI12, 101(VI00) 
	NOP   	 			NOP   
	NOP   	 			NOP   
	NOP   	 			NOP   
;
START: 	NOP		 		ISUBIU VI01, VI12, 4
	  ITOF12.w VF08w, VF06w 	LQ.xyzw VF31, 0(VI05)		;load GIFtag 
	NOP   	 			  IADDIU VI04, VI00, 0x0098	;set PRMODE COMMAND
	NOP   	 			  MFIR.x VF07, VI04
	NOP				  IADDIU VI04, VI00, 0x001b
	  ADDw.x VF09x, VF00x, VF08w 	  MFIR.z VF07, VI04
	NOP   	 			SQ.xyzw VF31, 0(VI01) 		;store GIFtag
	MULw.xyzw   VF17, VF00, VF00	ISW.x VI01, 35(VI00) 		;VF17=(1,?,?,0),save GIFtag address
;
;PROLOG
;
	NOP   	 			LQI.xyzw   VF01, (VI07++)	;VF01= XYZ
	NOP   	 			NOP
	  SUB.w      VF28w,VF00w,VF00w	NOP   
	NOP				NOP 				
	MULAx.xyzw  ACC, VF20, VF01x	LQI.xyzw   VF02, (VI08++)       ;[PRIM/LOCAL]*(XYZ),VF02= normal   
	MADDAy.xyzw ACC, VF21, VF01y	NOP
	MADDAz.xyzw ACC, VF22, VF01z	NOP   
	MADDw.xyzw  VF31,VF23, VF01w	NOP			   	;VF03=UV
	MULAx.xyz   ACC, VF24, VF02	NOP   				;[Light/LOCAL]*(normal)
	MADDAy.xyz  ACC, VF25, VF02	NOP			   	;
	MADDz.xyz   VF17,VF26, VF02	NOP   
	  MULAx.xyz   ACC, VF10, VF01x	DIV 	  Q, VF00w, VF31w	;(u,v)*Q,Q=1/Z
	  MADDAy.xyz  ACC, VF11, VF01y	NOP   
	  MADDAz.xyz  ACC, VF12, VF01z	NOP   
	  MADDw.xyz   VF05,VF13, VF01w	NOP   
	MAXx.xyzw   VF17, VF17, VF00x	NOP   				;0-limiter
	  MULAx.xyz   ACC, VF10, VF02x 	NOP
	  MADDAy.xyz  ACC, VF11, VF02y 	NOP
	  MADDz.xyz   VF06, VF12, VF02z	LQI.xyzw   VF01, (VI07++)      ;VF01= XYZ   
	MULAx.xyzw ACC, VF27, VF17x	NOP				;[light color]*(1,ez,ey,ex)
	MADDw.xyzw VF16, VF30, VF17w	NOP				;ambient color
	MULq.xyzw  VF15, VF31, Q 	IADDI     VI11, VI11, -1	;(x,y,z)*Q
	  MUL.xyz   VF28,VF05,VF06	NOP
	NOP				NOP
	NOP				NOP
	  ADDz.x     VF08x,VF00x,VF05z	NOP
	NOP				NOP
;
LOOP:	 MULAx.xyzw  ACC, VF20, VF01x	 LQI.xyzw   VF02, (VI08++)      ;[PRIM/LOCAL]*(XYZ),VF02= normal   
	 MADDAy.xyzw ACC, VF21, VF01y	  ESUM P, VF28
	 MADDAz.xyzw ACC, VF22, VF01z	NOP   
	 MADDw.xyzw  VF31,VF23, VF01w	LQI.xyzw  VF03, (VI10++)   	;VF03=UV
	  MULAx.xyz   ACC, VF10, VF01x	NOP
	  MADDAy.xyz  ACC, VF11, VF01y  NOP
	  MADDAz.xyz  ACC, VF12, VF01z	  IBNE VI04, VI13, AA1_ON
	  MADDw.xyz   VF05,VF13, VF01w	  IADDIU VI15, VI00, 0x0098
	 NOP				  IADDIU VI15, VI00, 0x0018
AA1_ON:  NOP				  MFIR.x VF07, VI15
	 MULAx.xyz   ACC, VF24, VF02	SQ.xyzw   VF14, -5(VI12)        ;[Light/LOCAL]*(normal),store RGBA
	 MADDAy.xyz  ACC, VF25, VF02	LQI.xyzw   VF04, (VI06++)   	;
	 MADDz.xyz   VF17,VF26, VF02	NOP
	  MULAx.xyz   ACC, VF10, VF02x   NOP
	  MADDAy.xyz  ACC, VF11, VF02y   NOP
	  MADDz.xyz   VF06, VF12, VF02z   MFP.x VF29x, P
	MULq.xyz   VF18, VF03, Q	 DIV 	  Q, VF00w, VF31w	;(u,v)*Q,Q=1/Z
	FTOI4.xyzw VF15, VF15		NOP   
	MUL.xyzw   VF16, VF16, VF04	  SQ.xyzw   VF07, -3(VI12)	;store PRMODE
	 MAXx.xyzw   VF17, VF17, VF00x	NOP   				;0-limiter
	  ABS.x VF29x, VF29x		NOP
	 NOP				NOP
	 NOP				NOP
	 NOP				NOP
	  MULAx.x ACCx, VF29x, VF09x	NOP
	  MUL.xyz   VF28,VF05,VF06 	SQ.xyzw   VF18, -2(VI12)      	;store STQ
	 NOP			 	SQ.xyzw   VF15, 0(VI12)   	;store XYZ
	 NOP				NOP
	  MSUBAw.x ACCx, VF08x, VF00w	NOP
	 NOP				NOP
	 NOP				NOP
	 NOP				  IADDIU VI04, VI00,0x80
	  ADDz.x     VF08x,VF00x,VF05z    FMAND VI13, VI04
	MINI.xyzw  VF14, VF16, VF19	NOP
	 MULAx.xyzw ACC, VF27, VF17x	IADDI     VI11, VI11, -1	;[light color]*(1,ez,ey,ex)
	 MADDw.xyzw VF16, VF30, VF17w	 LQI.xyzw   VF01, (VI07++)	;ambient color,VF01=XYZ
	 MULq.xyzw  VF15, VF31, Q 	IBNE      VI11, VI00, LOOP	;(x,y,z)*Q
	FTOI0.xyzw VF14, VF14 	  	IADDIU    VI12, VI12, 4
;
;EPILOG
;
	NOP				 LQI.xyzw  VF03, (VI10++)       ;VF03=UV
	NOP				NOP
	NOP				  IBNE VI04, VI13, AA1_ON1
	NOP				  IADDIU VI15, VI00, 0x0098
	NOP				  IADDIU VI15, VI00, 0x0018
AA1_ON1: NOP				  MFIR.x VF07, VI15
	NOP				NOP
	NOP				 LQI.xyzw   VF04, (VI06++)   	;
	NOP				SQ.xyzw   VF14, -5(VI12)        ;store RGBA
	 MULq.xyz   VF18, VF03, Q	NOP				;(u,v)*Q,Q=1/Z
	 FTOI4.xyzw VF15, VF15		  SQ.xyzw   VF07, -3(VI12)      ;store PRMODE
	 MUL.xyzw   VF16, VF16, VF04	NOP   
	NOP				NOP   				;0-limiter
	NOP   	 			 SQ.xyzw   VF18, -2(VI12)      	;store STQ
	NOP   	 			 SQ.xyzw   VF15, 0(VI12)   	;store XYZ
	 MINI.xyzw  VF14, VF16, VF19	NOP   
	NOP				NOP				;[light color]*(1,ez,ey,ex)
	NOP				NOP				;ambient color
	NOP 				NOP				;(x,y,z)*Q
	 FTOI0.xyzw VF14, VF14 	NOP
	NOP   	 			NOP
	NOP   	 			NOP
	NOP   	 			NOP
	NOP   	 			 SQ.xyzw   VF14, -1(VI12) 	;store RGBA
/****/
;
	NOP   	 			ILW.x VI03, 35(VI00) 
	NOP   	 			NOP   
	NOP   	 			NOP   
	NOP   	 			NOP   
	NOP   	 			XGKICK VI03  
	NOP   	 			IADDIU VI01, VI00, 1
	NOP   	 			ISUB VI09, VI01, VI09
	NOP   	 			NOP   
	NOP[e]   	 		NOP	
	NOP   	 			NOP   
	NOP   	 			B START_P  
	NOP   	 			NOP   
;
;library
;
MulMatrix: NOP   	 SQD.xyzw VF04, (--VI14) 
	 NOP   	 SQD.xyzw VF05, (--VI14) 
	 NOP   	 SQD.xyzw VF06, (--VI14) 
	 NOP   	 SQD.xyzw VF07, (--VI14) 
	 NOP   	 SQD.xyzw VF08, (--VI14) 
	 NOP   	 SQD.xyzw VF09, (--VI14) 
	 NOP   	 SQD.xyzw VF10, (--VI14) 
	 NOP   	 SQD.xyzw VF11, (--VI14) 
	 NOP   	 SQD.xyzw VF12, (--VI14) 
	 NOP   	 SQD.xyzw VF13, (--VI14) 
	 NOP   	 SQD.xyzw VF14, (--VI14) 
	 NOP   	 SQD.xyzw VF15, (--VI14) 
	 NOP   	 ISUBIU VI14, VI14, 1
	 NOP   	 ISW.x VI01, 0(VI14) 
	 NOP   	 ISUBIU VI14, VI14, 1
	 NOP   	 ISW.x VI02, 0(VI14) 
	 NOP   	 ISUBIU VI14, VI14, 1
	 NOP   	 ISW.x VI03, 0(VI14) 
	 NOP   	 LQI.xyzw VF08, (VI02++) 
	 NOP   	 LQI.xyzw VF04, (VI01++) 
	 NOP   	 LQI.xyzw VF05, (VI01++) 
	 NOP   	 LQI.xyzw VF06, (VI01++) 
	 NOP   	 LQI.xyzw VF07, (VI01++) 
	 MULAx.xyzw ACC, VF04, VF08x	 LQI.xyzw VF09, (VI02++) 
	 MADDAy.xyzw ACC, VF05, VF08y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF08z	 NOP   
	 MADDw.xyzw VF12, VF07, VF08w	 NOP   
	 MULAx.xyzw ACC, VF04, VF09x	 LQI.xyzw VF10, (VI02++) 
	 MADDAy.xyzw ACC, VF05, VF09y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF09z	 NOP   
	 MADDw.xyzw VF13, VF07, VF09w	 SQI.xyzw VF12, (VI03++) 
	 MULAx.xyzw ACC, VF04, VF10x	 LQI.xyzw VF11, (VI02++) 
	 MADDAy.xyzw ACC, VF05, VF10y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF10z	 NOP   
	 MADDw.xyzw VF14, VF07, VF10w	 SQI.xyzw VF13, (VI03++) 
	 MULAx.xyzw ACC, VF04, VF11x	 NOP   
	 MADDAy.xyzw ACC, VF05, VF11y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF11z	 NOP   
	 MADDw.xyzw VF15, VF07, VF11w	 SQI.xyzw VF14, (VI03++) 
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 SQI.xyzw VF15, (VI03++) 
	 NOP   	 ILW.x VI03, 0(VI14) 
	 NOP   	 IADDIU VI14, VI14, 1
	 NOP   	 ILW.x VI02, 0(VI14) 
	 NOP   	 IADDIU VI14, VI14, 1
	 NOP   	 ILW.x VI01, 0(VI14) 
	 NOP   	 IADDIU VI14, VI14, 1
	 NOP   	 LQI.xyzw VF15, (VI14++) 
	 NOP   	 LQI.xyzw VF14, (VI14++) 
	 NOP   	 LQI.xyzw VF13, (VI14++) 
	 NOP   	 LQI.xyzw VF12, (VI14++) 
	 NOP   	 LQI.xyzw VF11, (VI14++) 
	 NOP   	 LQI.xyzw VF10, (VI14++) 
	 NOP   	 LQI.xyzw VF09, (VI14++) 
	 NOP   	 LQI.xyzw VF08, (VI14++) 
	 NOP   	 LQI.xyzw VF07, (VI14++) 
	 NOP   	 LQI.xyzw VF06, (VI14++) 
	 NOP   	 LQI.xyzw VF05, (VI14++) 
	 NOP   	 LQI.xyzw VF04, (VI14++) 
	 NOP   	 NOP   
	 NOP   	 JR VI15  
	 NOP   	 NOP   
RotMatrix: NOP   	 SQD.xyzw VF01, (--VI14) 
	 NOP   	 SQD.xyzw VF02, (--VI14) 
	 NOP   	 SQD.xyzw VF03, (--VI14) 
	 NOP   	 SQD.xyzw VF04, (--VI14) 
	 NOP   	 SQD.xyzw VF05, (--VI14) 
	 NOP   	 SQD.xyzw VF06, (--VI14) 
	 NOP   	 SQD.xyzw VF07, (--VI14) 
	 NOP   	 SQD.xyzw VF08, (--VI14) 
	 NOP   	 SQD.xyzw VF09, (--VI14) 
	 NOP   	 SQD.xyzw VF10, (--VI14) 
	 NOP   	 SQD.xyzw VF11, (--VI14) 
	 NOP   	 SQD.xyzw VF12, (--VI14) 
	 NOP   	 ISUBIU VI14, VI14, 1
	 NOP   	 ISW.x VI01, 0(VI14) 
	 NOP   	 ISUBIU VI14, VI14, 1
	 NOP   	 ISW.x VI02, 0(VI14) 
	 MULx.xyzw VF04, VF00, VF00x	 LQI.xyzw VF01, (VI01++) 
	 MULx.xyzw VF05, VF00, VF00x	 LQI.xyzw VF02, (VI01++) 
	 MULx.xyzw VF06, VF00, VF00x	 LQI.xyzw VF03, (VI01++) 
	 NOP   	 LOI 1.5707963  
	 ADDw.x VF04, VF04, VF00w	 ESIN P, VF01x 
	 NOP   	 NOP   
	 NOP   	 NOP   
	 ADDi.xyzw VF02, VF02, I	 NOP   
	 NOP   	 WAITP   
	 NOP   	 MFP.z VF05z, P 
	 NOP   	 MFP.y VF06y, P 
	 NOP   	 ESIN P, VF02x 
	 NOP   	 NOP   
	 NOP   	 NOP   
	 MULz.xyzw VF06, VF06, VF00z	 NOP   
	 NOP   	 WAITP   
	 NOP   	 MFP.y VF05y, P 
	 NOP   	 MFP.z VF06z, P 
	 MULx.xyzw VF07, VF00, VF00x	 ESIN P, VF01y 
	 MULx.xyzw VF08, VF00, VF00x	 NOP   
	 MULx.xyzw VF09, VF00, VF00x	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 ADDw.y VF08, VF08, VF00w	 NOP   
	 NOP   	 WAITP   
	 NOP   	 MFP.z VF07z, P 
	 NOP   	 MFP.x VF09x, P 
	 NOP   	 ESIN P, VF02y 
	 NOP   	 NOP   
	 MULz.xyzw VF07, VF07, VF00z	 NOP   
	 NOP   	 WAITP   
	 NOP   	 MFP.x VF07x, P 
	 NOP   	 MFP.z VF09z, P 
	 MULx.xyzw VF10, VF00, VF00x	 ESIN P, VF01z 
	 MULx.xyzw VF11, VF00, VF00x	 NOP   
	 MULx.xyzw VF12, VF00, VF00x	 NOP   
	 MULAx.xyz ACC, VF04, VF07x	 NOP   
	 MADDAy.xyz ACC, VF05, VF07y	 NOP   
	 MADDz.xyz VF07, VF06, VF07z	 NOP   
	 ADDw.z VF12, VF12, VF00w	 NOP   
	 MULAx.xyz ACC, VF04, VF08x	 NOP   
	 MADDAy.xyz ACC, VF05, VF08y	 NOP   
	 MADDz.xyz VF08, VF06, VF08z	 NOP   
	 MULAx.xyz ACC, VF04, VF09x	 NOP   
	 MADDAy.xyz ACC, VF05, VF09y	 NOP   
	 MADDz.xyz VF09, VF06, VF09z	 NOP   
	 NOP   	 WAITP   
	 NOP   	 MFP.y VF10y, P 
	 NOP   	 MFP.x VF11x, P 
	 NOP   	 ESIN P, VF02z 
	 NOP   	 NOP   
	 NOP   	 NOP   
	 MULz.xyzw VF11, VF11, VF00z	 NOP   
	 NOP   	 WAITP   
	 NOP   	 MFP.x VF10x, P 
	 NOP   	 MFP.y VF11y, P 
	 NOP   	 NOP   
	 NOP   	 NOP   
	 MULAx.xyz ACC, VF07, VF10x	 NOP   
	 MADDAy.xyz ACC, VF08, VF10y	 NOP   
	 MADDz.xyz VF10, VF09, VF10z	 NOP   
	 MULAx.xyz ACC, VF07, VF11x	 NOP   
	 MADDAy.xyz ACC, VF08, VF11y	 NOP   
	 MADDz.xyz VF11, VF09, VF11z	 NOP   
	 MULAx.xyz ACC, VF07, VF12x	 NOP   
	 MADDAy.xyz ACC, VF08, VF12y	 NOP   
	 MADDz.xyz VF12, VF09, VF12z	 NOP   
	 NOP   	 NOP   
	 NOP   	 SQI.xyz VF10, (VI02++) 
	 NOP   	 SQI.xyz VF11, (VI02++) 
	 NOP   	 SQI.xyz VF12, (VI02++) 
	 NOP   	 SQI.xyz VF03, (VI02++) 
	 NOP   	 ILW.x VI02, 0(VI14) 
	 NOP   	 IADDIU VI14, VI14, 1
	 NOP   	 ILW.x VI01, 0(VI14) 
	 NOP   	 IADDIU VI14, VI14, 1
	 NOP   	 LQI.xyzw VF12, (VI14++) 
	 NOP   	 LQI.xyzw VF11, (VI14++) 
	 NOP   	 LQI.xyzw VF10, (VI14++) 
	 NOP   	 LQI.xyzw VF09, (VI14++) 
	 NOP   	 LQI.xyzw VF08, (VI14++) 
	 NOP   	 LQI.xyzw VF07, (VI14++) 
	 NOP   	 LQI.xyzw VF06, (VI14++) 
	 NOP   	 LQI.xyzw VF05, (VI14++) 
	 NOP   	 LQI.xyzw VF04, (VI14++) 
	 NOP   	 LQI.xyzw VF03, (VI14++) 
	 NOP   	 LQI.xyzw VF02, (VI14++) 
	 NOP   	 LQI.xyzw VF01, (VI14++) 
	 NOP   	 NOP   
	 NOP   	 JR VI15  
	 NOP   	 NOP   
ApplyMatrix4: NOP   	 SQD.xyzw VF01, (--VI14) 
	 NOP   	 SQD.xyzw VF02, (--VI14) 
	 NOP   	 SQD.xyzw VF03, (--VI14) 
	 NOP   	 SQD.xyzw VF04, (--VI14) 
	 NOP   	 SQD.xyzw VF05, (--VI14) 
	 NOP   	 ISUBIU VI14, VI14, 1
	 NOP   	 ISW.x VI02, 0(VI14) 
	 NOP   	 LQ.xyzw VF01, 0(VI01) 
	 NOP   	 LQI.xyzw VF02, (VI02++) 
	 NOP   	 LQI.xyzw VF03, (VI02++) 
	 NOP   	 LQI.xyzw VF04, (VI02++) 
	 NOP   	 LQI.xyzw VF05, (VI02++) 
	 MULAx.xyzw ACC, VF02, VF01x	 NOP   
	 MADDAy.xyzw ACC, VF03, VF01y	 NOP   
	 MADDAz.xyzw ACC, VF04, VF01z	 NOP   
	 MADDw.xyzw VF31, VF05, VF01w	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 SQ.xyzw VF31, 0(VI03) 
	 NOP   	 ILW.x VI02, 0(VI14) 
	 NOP   	 IADDIU VI14, VI14, 1
	 NOP   	 LQI.xyzw VF05, (VI14++) 
	 NOP   	 LQI.xyzw VF04, (VI14++) 
	 NOP   	 LQI.xyzw VF03, (VI14++) 
	 NOP   	 LQI.xyzw VF02, (VI14++) 
	 NOP   	 LQI.xyzw VF01, (VI14++) 
	 NOP   	 NOP   
	 NOP   	 JR VI15  
	 NOP   	 NOP   
ApplyMatrix3: NOP   	 SQD.xyzw VF01, (--VI14) 
	 NOP   	 SQD.xyzw VF02, (--VI14) 
	 NOP   	 SQD.xyzw VF03, (--VI14) 
	 NOP   	 SQD.xyzw VF04, (--VI14) 
	 NOP   	 ISUBIU VI14, VI14, 1
	 NOP   	 ISW.x VI02, 0(VI14) 
	 MULw.xyzw VF31, VF00, VF00	 LQ.xyzw VF01, 0(VI01) 
	 NOP   	 LQI.xyzw VF02, (VI02++) 
	 NOP   	 LQI.xyzw VF03, (VI02++) 
	 NOP   	 LQI.xyzw VF04, (VI02++) 
	 MULAx.xyz ACC, VF02, VF01	 NOP   
	 MADDAy.xyz ACC, VF03, VF01	 NOP   
	 MADDz.xyz VF31, VF04, VF01	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 SQ.xyzw VF31, 0(VI03) 
	 NOP   	 ILW.x VI02, 0(VI14) 
	 NOP   	 IADDIU VI14, VI14, 1
	 NOP   	 LQI.xyzw VF04, (VI14++) 
	 NOP   	 LQI.xyzw VF03, (VI14++) 
	 NOP   	 LQI.xyzw VF02, (VI14++) 
	 NOP   	 LQI.xyzw VF01, (VI14++) 
	 NOP   	 NOP   
	 NOP   	 JR VI15  
	 NOP   	 NOP   
;
