/* SCEI CONFIDENTIAL
 "PlayStation 2" Programmer Tool Runtime Library  Release 2.0
 */
/* 
 *              Emotion Engine Library Sample Program
 *
 *                         - deform -
 *
 *                         Version 0.1.0
 *                           Shift-JIS
 *
 *      Copyright (C) 1998-1999 Sony Computer Entertainment Inc.
 *                        All Rights Reserved.
 *
 *      Name : mime.dsm
 *      Description : vu1 micro code to deform, rotate, illuminate, perspective-transform object.
 *
 *       Version        Date            Design      Log
 *  --------------------------------------------------------------------
 *      0.1.0           Mar,26,1999     munekis    1st version
 */
	;
	 NOP   	 IADDIU VI01, VI00, 512 ; screen matrix pointer
	 NOP   	 IADDIU VI02, VI00, 504 ; rotation matrix pointer
	 NOP   	 IADDIU VI03, VI00, 520 ; ?
	 NOP   	 NOP   
	 NOP   	 BAL VI15, MulMatrix    ; create ROT x SCREEN matrix into VF12 - VF15
	 NOP   	 NOP   
	 NOP   	 IADDIU VI01, VI00, 520 ; ROT x SCREEN matrix
	 NOP   	 IADDIU VI02, VI00, 496 ; COLOR matrix
	 NOP   	 IADDIU VI03, VI00, 500 ; LIGHT matrix
	 NOP   	 IADDIU VI04, VI00, 525 ; COLOR MAX/MIN
	 NOP   	 IADDIU VI05, VI00, 495 ; ?
	 NOP   	 IADDIU VI08, VI00, 526 ; ?
	 NOP   	 IADDIU VI10, VI00, 494 ; WEIGHT VECTOR
	 NOP	 LQI.xyzw VF16, (VI10++) 
	 NOP   	 LQI.xyzw VF04, (VI01++) ; load ROT x SCREEN matrix 
	 NOP   	 LQI.xyzw VF05, (VI01++) 
	 NOP   	 LQI.xyzw VF06, (VI01++) 
	 NOP   	 LQI.xyzw VF07, (VI01++) 
	 NOP   	 LQI.xyzw VF08, (VI02++) ; load COLOR matrix
	 NOP   	 LQI.xyzw VF09, (VI02++) 
	 NOP   	 LQI.xyzw VF10, (VI02++) 
	 NOP   	 LQI.xyzw VF11, (VI02++) 
	 NOP   	 LQI.xyzw VF12, (VI03++) ; load LIGHT matrix 
	 NOP   	 LQI.xyzw VF13, (VI03++) 
	 NOP   	 LQI.xyzw VF14, (VI03++) 
	 NOP   	 LQI.xyzw VF15, (VI03++) 
	 NOP   	 LQI.xyzw VF01, (VI04++) ; load COLOR MAX/MIN
	 NOP   	 NOP   
	 NOP[E]   	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
START0: 
	 NOP   	 XTOP VI06 		; load TOP reg (actually "0") 
	 NOP	 IADD VI08, VI00, VI06  ; copy it to VI08
	 NOP   	 ILWR.x VI11, (VI06)x   ; Load GIF Packet value(to get nb of vertices).
	 NOP	 IADDIU VI07, VI00, 0x7fff; mask nb of "NLOOP" in GIF packet
	 NOP	 IAND VI11, VI07, VI11 ; mask it. we have nb triangles
	 NOP   	 IADD VI07, VI11, VI11
	 NOP   	 IADD VI11, VI07, VI11 ; just doing "x3" ;-). total of vertices
	 NOP   	 IADDIU VI07, VI06, 121 ; sets the pointer for normal.
	 NOP   	 IADDIU VI10, VI06, 181 ; sets the pointer for uv.
	 NOP   	 IADDIU VI05, VI06, 241 ; sets the pointer for rgba
	 NOP   	 IADDIU VI09, VI06, 533 ; pointer for XGKICK 
	 NOP   	 IADDIU VI12, VI06, 533 ; pointer for XYZF2
	 NOP   	 IADDIU VI13, VI06, 531 ; pointer for RGBAQ
	 NOP   	 IADDIU VI14, VI06, 532 ; pointer for ST
	 NOP   	 IADDIU VI06, VI06, 1   ; sets the pointer for vertices.

START: 
	 NOP   	 LQI.xyzw VF17, (VI06++) ; load vertices.
	 NOP   	 LQI VF18, (VI06++)
	 NOP   	 LQI.xyzw VF25, (VI07++) ; load normal. 
	 ADDw.w VF26, VF00, VF00w	 LQ.xyzw VF02, 0(VI05) 
	 MULAx.xyzw ACC, VF17, VF16x     NOP
	 MADDy.xyzw VF30, VF18, VF16y    NOP
	 MULAx.xyz ACC, VF12, VF25x	 NOP   
	 MADDAy.xyz ACC, VF13, VF25y	 NOP   
	 MADDz.xyz VF26, VF14, VF25z	 NOP   
	 MULAx.xyzw ACC, VF04, VF30x	 NOP   
	 MADDAy.xyzw ACC, VF05, VF30y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF30z	 NOP   
	 MADDw.xyzw VF31, VF07, VF30w	 NOP   
	 MAXx.xyzw VF26, VF26, VF00x	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
LOOP: 
	 MULw.xyzw VF29, VF31, VF00w	 DIV Q, VF00w, VF31w
	 MULAx.xyzw ACC, VF08, VF26x	 LQI.xyzw VF25, (VI07++) 
	 MADDAy.xyzw ACC, VF09, VF26y	 LQI.xyzw VF17, (VI06++) 
	 MADDAz.xyzw ACC, VF10, VF26z	 LQI.xyzw VF18, (VI06++)
	 MADDw.xyzw VF24, VF11, VF26w	 IADDI VI11, VI11, -1
	 MULAx.xyz ACC, VF12, VF25x	 IADDIU VI12, VI12, 3
	 MADDAy.xyz ACC, VF13, VF25y	 SQ.xyzw VF23, 0(VI13) 
	 MULq.xyzw VF28, VF29, Q	 IADDIU VI13, VI13, 3
	 MUL.xyzw VF24, VF24, VF02	 IADDIU VI14, VI14, 3
	 MADDz.xyz VF26, VF14, VF25z	 LQ.xyzw VF02, 0(VI05) 
	 MULAx.xyzw ACC, VF17, VF16x     LQI.xyzw VF19, (VI10++)
	 MADDy.xyzw VF30, VF18, VF16y    NOP
	 FTOI4.xyzw VF27, VF28           NOP
	 MINI.xyzw VF24, VF24, VF01      NOP
	 NOP				 SQ.xyzw VF19, 0(VI14)
	 MULAx.xyzw ACC, VF04, VF30x	 NOP   
	 MADDAy.xyzw ACC, VF05, VF30y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF30z	 NOP   
	 MADDw.xyzw VF31, VF07, VF30w	 SQ.xyzw VF27, 0(VI12) 
	 FTOI0.xyzw VF23, VF24 	 NOP   
	 MAXx.xyzw VF26, VF26, VF00x	 IBNE VI11, VI00, LOOP
	 NOP   	 NOP   
	 MULw.xyzw VF29, VF31, VF00w	 DIV Q, VF00w, VF31w
	 MULAx.xyzw ACC, VF08, VF26x	 IADDIU VI12, VI12, 3
	 MADDAy.xyzw ACC, VF09, VF26y	 SQ.xyzw VF23, 0(VI13) 
	 MADDAz.xyzw ACC, VF10, VF26z	 IADDIU VI13, VI13, 3
	 MADDw.xyzw VF24, VF11, VF26w	 LQ.xyzw VF20, 0(VI08) 
	 NOP   	 NOP			    
	 NOP   	 NOP   
	 MULq.xyzw VF28, VF29, Q	 NOP   
	 MUL.xyzw VF24, VF24, VF02	 SQ.xyzw VF20, 0(VI09) 
	 NOP   	 NOP   
	 NOP   	 NOP   
	 FTOI4.xyzw VF27, VF28 	 NOP   
	 MINI.xyzw VF24, VF24, VF01	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 SQ.xyzw VF27, 0(VI12) 
	 FTOI0.xyzw VF23, VF24 	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 SQ.xyzw VF23, 0(VI13) 
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 XGKICK VI09  
	 NOP[E]   	 NOP   
	 NOP   	 NOP   
	 NOP   	 B START0  
	 NOP   	 NOP   
MulMatrix: 
	 NOP   	 LQI.xyzw VF08, (VI02++) 
	 NOP   	 LQI.xyzw VF04, (VI01++) 
	 NOP   	 LQI.xyzw VF05, (VI01++) 
	 NOP   	 LQI.xyzw VF06, (VI01++) 
	 NOP   	 LQI.xyzw VF07, (VI01++) 
	 MULAx.xyzw ACC, VF04, VF08x	 LQI.xyzw VF09, (VI02++) 
	 MADDAy.xyzw ACC, VF05, VF08y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF08z	 NOP   
	 MADDw.xyzw VF12, VF07, VF08w	 NOP   
	 MULAx.xyzw ACC, VF04, VF09x	 LQI.xyzw VF10, (VI02++) 
	 MADDAy.xyzw ACC, VF05, VF09y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF09z	 NOP   
	 MADDw.xyzw VF13, VF07, VF09w	 SQI.xyzw VF12, (VI03++) 
	 MULAx.xyzw ACC, VF04, VF10x	 LQI.xyzw VF11, (VI02++) 
	 MADDAy.xyzw ACC, VF05, VF10y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF10z	 NOP   
	 MADDw.xyzw VF14, VF07, VF10w	 SQI.xyzw VF13, (VI03++) 
	 MULAx.xyzw ACC, VF04, VF11x	 NOP   
	 MADDAy.xyzw ACC, VF05, VF11y	 NOP   
	 MADDAz.xyzw ACC, VF06, VF11z	 NOP   
	 MADDw.xyzw VF15, VF07, VF11w	 SQI.xyzw VF14, (VI03++) 
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 NOP   
	 NOP   	 SQI.xyzw VF15, (VI03++) 
	 NOP   	 NOP   
	 NOP   	 JR VI15  
	 NOP   	 NOP   
