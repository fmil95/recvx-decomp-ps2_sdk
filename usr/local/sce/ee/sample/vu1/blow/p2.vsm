/* SCEI CONFIDENTIAL
 "PlayStation 2" Programmer Tool Runtime Library  Release 2.0
 */
/* 
 *              Emotion Engine Library Sample Program
 *
 *                         - blow -
 *
 *                         Version 0.3.0
 *                           Shift-JIS
 *
 *      Copyright (C) 1998-1999 Sony Computer Entertainment Inc.
 *                        All Rights Reserved.
 *
 *          Name : p2.vsm
 *          Description : vu1 micro code to draw particle shadow. 
 *
 *       Version        Date            Design      Log
 *  --------------------------------------------------------------------
 *       0.1.0          Mar,26,1999     munekis     first version
 */
; -------------------------------------
;
;         draw shadow particle micro
;
; -------------------------------------
	NOP	IADDIU VI01, VI00, 0 ; Perspecitive Matrix
	NOP	IADDIU VI02, VI00, 4 ; Offset Vector
	NOP	IADDIU VI03, VI00, 8 ; ST
;
	NOP	LQI.xyzw VF01, (VI01++) ; load Perspective Matrix
	NOP	LQI.xyzw VF02, (VI01++) 
	NOP	LQI.xyzw VF03, (VI01++) 
	NOP	LQI.xyzw VF04, (VI01++) 
	NOP	LQI.xyzw VF09, (VI03++) ; load ST
	NOP	LQI.xyzw VF10, (VI03++) 
	NOP	LQI.xyzw VF11, (VI03++) 
	NOP	LQI.xyzw VF12, (VI03++) 
	NOP	NOP
	NOP	NOP
	NOP	NOP
;
START02:
	NOP	XTOP VI04
	NOP	ILWR.x VI09, (VI04)x   ; Load GIF Packet value
	NOP	IADDIU VI07, VI00, 0x7fff; mask nb of "NLOOP"
	NOP	IAND VI09, VI07, VI09  ; mask. This is counter.
	NOP	IADD VI05, VI09, VI09   ; 
	NOP	IADD VI05, VI05, VI09   ; set offset (nb x 3)
	NOP	IADDIU VI07, VI04, 1   ; position & color
	NOP	IADD VI10, VI07, VI05 ; XGKICK
	NOP	IADDIU VI11, VI10, 1 ; RGBAQ
	NOP	IADDIU VI12, VI10, 2 ; ST
	NOP	IADDIU VI13, VI10, 3 ; XYZF2
	NOP	LQ.xyzw VF19, 0(VI04)  ; load gif tag
LOOP02:
	NOP	LQI.xyzw VF20, (VI07++) ; load position
	NOP	LQI.xyzw VF13, (VI07++) ; load scaling
	NOP	LQI.xyzw VF22, (VI07++) ; load RGBA
	NOP	LQ.xyzw VF23, 0(VI02) ; load offset vector
	NOP	LQ.xyzw VF24, 1(VI02) 
	NOP	LQ.xyzw VF25, 2(VI02) 
	NOP	LQ.xyzw VF26, 3(VI02) 
	MULx.xyzw VF23, VF23, VF13x	NOP
	MULx.xyzw VF24, VF24, VF13x	NOP
	MULx.xyzw VF25, VF25, VF13x	NOP
	MULx.xyzw VF26, VF26, VF13x	NOP
	NOP NOP
	NOP NOP
	NOP NOP
	NOP NOP
	NOP NOP
	ADD VF21, VF23, VF20		SQ.xyzw VF22, 0(VI11); store RGBAQ
	MULAx.xyzw ACC, VF01, VF21x     IADDIU VI11, VI11, 9 ; 
	MADDAy.xyzw ACC, VF02, VF21y    SQ.xyzw VF09, 0(VI12) ;
	MADDAz.xyzw ACC, VF03, VF21z    IADDIU VI12, VI12, 2 ;
	MADDw.xyzw VF05, VF04, VF21w    NOP;
	NOP NOP
	NOP NOP
	NOP NOP
	NOP	DIV Q, VF00w, VF05w	
	ADD VF21, VF24, VF20		NOP
	MULAx.xyzw ACC, VF01, VF21x     SQ.xyzw VF10, 0(VI12)  
	MADDAy.xyzw ACC, VF02, VF21y    IADDIU VI12, VI12, 2 
	MADDAz.xyzw ACC, VF03, VF21z    NOP
	MADDw.xyzw VF06, VF04, VF21w    NOP
	NOP 				SQ.xyzw VF11, 0(VI12)
	MULq VF05, VF05, Q		IADDIU VI12, VI12, 2
	NOP	DIV Q, VF00w, VF06w	
	ADD VF21, VF25, VF20		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	MULAx.xyzw ACC, VF01, VF21x     NOP
	MADDAy.xyzw ACC, VF02, VF21y    SQ.xyzw VF12, 0(VI12) ;
	MADDAz.xyzw ACC, VF03, VF21z    IADDIU VI12, VI12, 3 ;
	MADDw.xyzw VF07, VF04, VF21w    NOP ;
	NOP NOP
	MULq VF06, VF06, Q		NOP
	NOP	DIV Q, VF00w, VF07w	
	ADD VF21, VF26, VF20		NOP
	MULAx.xyzw ACC, VF01, VF21x     NOP ; -- pers-trans pos --
	MADDAy.xyzw ACC, VF02, VF21y    NOP ;
	MADDAz.xyzw ACC, VF03, VF21z    NOP ;
	MADDw.xyzw VF08, VF04, VF21w    NOP ;
	NOP NOP
	NOP NOP
	NOP NOP
	MULq VF07, VF07, Q		LOI -1.0
	NOP	DIV Q, VF00w, VF08w	

	MULi.w VF05w, VF05w, I		NOP
	MULi.w VF06w, VF06w, I		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	FTOI4 VF15, VF05		NOP
	FTOI4 VF16, VF06		NOP
	FTOI4 VF17, VF07		NOP
	NOP NOP
	NOP	                        SQ.xyzw VF15, 0(VI13)  ; store XYZF0 	
	NOP 	                        IADDIU VI13, VI13, 2 ;
	NOP 	                        SQ.xyzw VF16, 0(VI13)  ; store XYZF1
	MULq VF08, VF08, Q		IADDIU VI13, VI13, 2 ;
	NOP 	                        SQ.xyzw VF17, 0(VI13)
	NOP	                        IADDIU VI13, VI13, 2 
	FTOI4 VF18, VF08		NOP
	NOP NOP 
	NOP NOP 
	NOP NOP 
	NOP 	SQ.xyzw VF18, 0(VI13)
	NOP	IADDIU VI13, VI13, 3  

	NOP	IADDI VI09, VI09, -1
	NOP	NOP
	NOP	IBNE VI09, VI00, LOOP02
	NOP	NOP
	NOP	SQ.xyzw VF19, 0(VI10);
	NOP	NOP
	NOP	NOP
	NOP	NOP
	NOP	XGKICK VI10
	NOP[E]	NOP
	NOP	NOP
	NOP	B START02
	NOP	NOP
	NOP	NOP










