/* SCEI CONFIDENTIAL
 "PlayStation 2" Programmer Tool Runtime Library  Release 2.0
 */
/* 
 *              Emotion Engine Library Sample Program
 *
 *                         - blow -
 *
 *                         Version 0.3.0
 *                           Shift-JIS
 *
 *      Copyright (C) 1998-1999 Sony Computer Entertainment Inc.
 *                        All Rights Reserved.
 *
 *          Name : p.vsm
 *          Description : vu1 micro code to draw particle. 
 *
 *       Version        Date            Design      Log
 *  --------------------------------------------------------------------
 *       0.1.0          Mar,26,1999     munekis     first version
 */

; -------------------------------------
;
;          particle calc micro
;
; -------------------------------------
	NOP	IADDIU VI01, VI00, 0 ; Perspecitive Matrix
	NOP	IADDIU VI02, VI00, 4 ; Offset Vector
	NOP	IADDIU VI03, VI00, 8 ; ST
;
	NOP	LQI.xyzw VF01, (VI01++) ; load Perspective Matrix
	NOP	LQI.xyzw VF02, (VI01++) 
	NOP	LQI.xyzw VF03, (VI01++) 
	NOP	LQI.xyzw VF04, (VI01++) 
	NOP	LQI.xyzw VF23, (VI02++) ; load offset vector
	NOP	LQI.xyzw VF24, (VI02++) 
	NOP	LQI.xyzw VF25, (VI02++) 
	NOP	LQI.xyzw VF26, (VI02++) 
	NOP	LQI.xyzw VF09, (VI03++) ; load ST
	NOP	LQI.xyzw VF10, (VI03++) 
	NOP	LQI.xyzw VF11, (VI03++) 
	NOP	LQI.xyzw VF12, (VI03++) 
	; --- load gif tag ---
	NOP	IADDIU VI01, VI00, 12   
	NOP	LQ.xyzw VF19, 0(VI01)  

START0:
	NOP	XTOP VI04
	NOP	IADDIU VI09, VI00, 4  ; This is counter.
	NOP	IADD VI05, VI09, VI09    
	NOP	IADD VI05, VI05, VI09  ; set offset from data area(nb x 3)
	NOP	IADDIU VI07, VI04, 1   ; position & color
	NOP	IADD VI10, VI07, VI05 ; XGKICK
	NOP	IADDIU VI11, VI10, 1 ; RGBAQ
	NOP	IADDIU VI12, VI10, 2 ; ST
	NOP	IADDIU VI13, VI10, 3 ; XYZF2
	;-----------------------------------------------
LOOPA:
	NOP	IADDIU VI01, VI00, 10 ; nb of motion blur 
	NOP	LQI.xyzw VF20, (VI07++) ; load position
	NOP	LQI.xyzw VF14, (VI07++) ; load velocity
	NOP	LQI.xyzw VF22, (VI07++) ; load RGBA
	NOP NOP
LOOP:
	SUB VF20, VF20, VF14		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	ADD VF21, VF23, VF20		SQ.xyzw VF22, 0(VI11)  ; store RGBA0
	MULAx.xyzw ACC, VF01, VF21x     IADDIU VI11, VI11, 9 ; 
	MADDAy.xyzw ACC, VF02, VF21y    SQ.xyzw VF09, 0(VI12) ;
	MADDAz.xyzw ACC, VF03, VF21z    IADDIU VI12, VI12, 2 ;
	MADDw.xyzw VF05, VF04, VF21w    NOP;
	NOP NOP
	NOP NOP
	NOP NOP
	NOP	DIV Q, VF00w, VF05w	
	ADD VF21, VF26, VF20		NOP
	MULAx.xyzw ACC, VF01, VF21x     SQ.xyzw VF10, 0(VI12) ; 
	MADDAy.xyzw ACC, VF02, VF21y    IADDIU VI12, VI12, 2 ;
	MADDAz.xyzw ACC, VF03, VF21z    NOP
	MADDw.xyzw VF08, VF04, VF21w    NOP
	NOP 				SQ.xyzw VF11, 0(VI12)
	MULq VF05, VF05, Q		IADDIU VI12, VI12, 2
	NOP	DIV Q, VF00w, VF08w	
	NOP NOP
	NOP NOP
	NOP				SQ.xyzw VF12, 0(VI12) ;
	NOP				IADDIU VI12, VI12, 3 ;
	NOP				NOP ;
	NOP NOP
	MULq VF08, VF08, Q		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	;-------------------------------------------------------
	SUB VF13, VF08, VF05		LOI -1.0
	ADDx VF06, VF05, VF00x		NOP
	ADDx VF07, VF05, VF00x		NOP
	ADDx.x VF06x, VF06x, VF13x	NOP
	ADDy.y VF07y, VF07y, VF13y 	NOP
	NOP NOP
	MULi.w VF05w, VF05w, I		NOP
	MULi.w VF06w, VF06w, I		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	FTOI4 VF15, VF05		NOP
	FTOI4 VF16, VF06		NOP
	FTOI4 VF17, VF07		NOP
	FTOI4 VF18, VF08		NOP
	NOP	                        SQ.xyzw VF15, 0(VI13)  ; store XYZF0 	
	NOP 	                        IADDIU VI13, VI13, 2 ;
	NOP 	                        SQ.xyzw VF16, 0(VI13)  ; store XYZF1
	NOP				IADDIU VI13, VI13, 2 ;
	NOP 	                        SQ.xyzw VF17, 0(VI13)
	NOP	                        IADDIU VI13, VI13, 2 
	NOP 				SQ.xyzw VF18, 0(VI13)
	NOP				IADDIU VI13, VI13, 3  
	NOP	IADDI VI01, VI01, -1
	NOP	NOP
	NOP	IBNE VI01, VI00, LOOP
	NOP	NOP
	NOP	NOP
	;--------------------------------------------------------
	NOP	IADDI VI09, VI09, -1
	NOP	NOP
	NOP	IBNE VI09, VI00, LOOPA
	NOP	NOP
	;--------------------------------------------------------
	NOP	SQ.xyzw VF19, 0(VI10);
	NOP	NOP
	NOP	NOP
	NOP	NOP
	NOP	XGKICK VI10
	NOP[E]	NOP
	NOP	NOP
	NOP	B START0
	NOP	NOP
	NOP	NOP










