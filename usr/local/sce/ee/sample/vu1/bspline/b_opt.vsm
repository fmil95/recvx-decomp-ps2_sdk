/* SCEI CONFIDENTIAL
 "PlayStation 2" Programmer Tool Runtime Library  Release 2.0
 */
/* 
 *              Emotion Engine Library Sample Program
 *
 *                         - bspline -
 *
 *                         Version 0.3.0
 *                           Shift-JIS
 *
 *      Copyright (C) 1998-1999 Sony Computer Entertainment Inc.
 *                        All Rights Reserved.
 *
 *          Name : b_opt.vsm
 *          Description : vu1 micro code to draw b-spline surface(OPTIMIZED). 
 *
 *       Version        Date            Design      Log
 *  --------------------------------------------------------------------
 *       0.3.0          Jun,1,1999     munekis     first version
 */

; -------------------------------------------------------------------
;
;
;	VF01 - VF16 : Control points
;	VF17 - VF19 : Blending Coeff.
; 	VF20 - VF27 : tmp
;	VF28 - VF31 : perspective matrix
;
;	VI01, VI02 : counter for U, V
;	VI03 : div U, V (same number)
;       VI04 : RGBAQ store pointer
;	VI09 : ST store pointer
;       VI05 : XYZF store pointer
;	VI06 : source color pointer
;	VI10 : load pointer for control points set
;	VI11 : counter (number of 4x4 control points sets)
;
;	vumem mapping
; 	0 : giftag
;       1 - 16 : control point
;       17 - 20 : blending coeff(0.0, 0.333, 0.666, 1.00).
;       21 - 24 : perspective matrix
;	25 : source color
;	100 : XGKICK area
;
;
;
; -------------------------------------------------------------------

.vu
	;--- load perspective matrix ---
	NOP     IADDIU VI02, VI00, 0
	NOP	LQI VF28, (VI02++)
	NOP	LQI VF29, (VI02++)
	NOP	LQI VF30, (VI02++)
	NOP	LQI VF31, (VI02++)
	NOP[E]	NOP
	NOP 	NOP
START0:
	;--- load control points ---
	NOP	XTOP VI07 
	NOP     IADDIU VI06, VI07, 0

	NOP	LQ VF17, 0(VI06) 	; load gif tag

	NOP	ILWR.x VI11, (VI06)x	;  
	NOP	IADDIU VI10, VI00, 0x7fff
	NOP	IAND VI10, VI10, VI11	; nloop
	NOP	IADDIU VI11, VI00, 0	;
NLOOP:
	NOP	ISUBIU VI10, VI10, 24
	NOP	IADDIU VI11, VI11, 1
	NOP	IBNE VI10, VI00, NLOOP
	NOP	NOP

	; --- load 1st control points set ---
	NOP     IADDIU VI10, VI07, 1 ; -- pointer to base ST & load control point --
	NOP	LQI VF27, (VI10++)   ; -- base ST --
	NOP	LQI VF05, (VI10++)   ; -- control point --
	NOP	LQI VF06, (VI10++)
	NOP	LQI VF07, (VI10++)
	NOP	LQI VF08, (VI10++)
	NOP	LQI VF09, (VI10++)
	NOP	LQI VF10, (VI10++)
	NOP	LQI VF11, (VI10++)
	NOP	LQI VF12, (VI10++)
	NOP	LQI VF13, (VI10++)
	NOP	LQI VF14, (VI10++)
	NOP	LQI VF15, (VI10++)
	NOP	LQI VF16, (VI10++)

	; --- store gif tag ---
	NOP     IADDIU VI06, VI07, 35
	NOP	SQ VF17, 0(VI06) 

	;--- set pointer to store RGBA/ST/XYZF
	NOP	IADDIU VI04, VI07, 36
	NOP	IADDIU VI09, VI07, 37
	NOP	IADDIU VI05, VI07, 38
	NOP     IADDIU VI02, VI11, 0

LOOP02:
	ADDx.xyzw VF01, VF05, VF00x	IADDIU VI01, VI00, 0
	ADDx.xyzw VF02, VF06, VF00x	IADDIU VI03, VI00, 24
	ADDx.xyzw VF03, VF07, VF00x	IADDIU VI08, VI00, 10
	ADDx.xyzw VF04, VF08, VF00x	NOP

	ADDx.xyzw VF05, VF09, VF00x	LQ VF17, 0(VI08)
	ADDx.xyzw VF06, VF10, VF00x	LQ VF18, 3(VI08)
	ADDx.xyzw VF07, VF11, VF00x	NOP
	ADDx.xyzw VF08, VF12, VF00x	NOP

	ADDx.xyzw VF09, VF13, VF00x	NOP
	ADDx.xyzw VF10, VF14, VF00x	NOP
	ADDx.xyzw VF11, VF15, VF00x	NOP
	ADDx.xyzw VF12, VF16, VF00x	NOP

	MULAx.xyzw  ACC,  VF01, VF17x	LQI VF13, (VI10++)
	MADDAy.xyzw ACC,  VF02, VF17y   LQI VF14, (VI10++)
	MADDAz.xyzw ACC,  VF03, VF17z   LQI VF15, (VI10++)
	MADDw.xyzw  VF21, VF04, VF17w   LQI VF16, (VI10++)
	
	MULAx.xyzw  ACC,  VF05, VF17x	NOP 
	MADDAy.xyzw ACC,  VF06, VF17y   NOP
	MADDAz.xyzw ACC,  VF07, VF17z   NOP
	MADDw.xyzw  VF22, VF08, VF17w   NOP

	MULAx.xyzw  ACC,  VF09, VF17x	NOP 
	MADDAy.xyzw ACC,  VF10, VF17y   NOP
	MADDAz.xyzw ACC,  VF11, VF17z   NOP
	MADDw.xyzw  VF23, VF12, VF17w   NOP

	; --- calc vector1 ---
	MULAx.xyzw  ACC,  VF13, VF17x	NOP 
	MADDAy.xyzw ACC,  VF14, VF17y   NOP
	MADDAz.xyzw ACC,  VF15, VF17z   NOP
	MADDw.xyzw  VF24, VF16, VF17w   NOP

	MULAx.xyzw  ACC,  VF21, VF18x	NOP 
	MADDAy.xyzw ACC,  VF22, VF18y   NOP
	MADDAz.xyzw ACC,  VF23, VF18z   NOP
	MADDw.xyzw  VF25, VF24, VF18w   LQ VF17, 1(VI08)

	NOP NOP
	NOP NOP
	NOP NOP

        MULAx.xyzw  ACC,  VF01, VF17x   NOP
        MADDAy.xyzw ACC,  VF02, VF17y   NOP
        MADDAz.xyzw ACC,  VF03, VF17z   NOP
        MADDw.xyzw  VF21, VF04, VF17w   NOP

        MULAx.xyzw  ACC,  VF05, VF17x   NOP
        MADDAy.xyzw ACC,  VF06, VF17y   NOP
        MADDAz.xyzw ACC,  VF07, VF17z   NOP
        MADDw.xyzw  VF22, VF08, VF17w   NOP

        MULAx.xyzw  ACC,  VF09, VF17x   NOP
        MADDAy.xyzw ACC,  VF10, VF17y   NOP
        MADDAz.xyzw ACC,  VF11, VF17z   NOP

LOOP01:
        MADDw.xyzw  VF23, VF12, VF17w   	NOP
        MULAx.xyzw  ACC,  VF13, VF17x   	LQ VF18, 2(VI08)
        MADDAy.xyzw ACC,  VF14, VF17y   	NOP
        MADDAz.xyzw ACC,  VF15, VF17z   	NOP
        MADDw.xyzw  VF24, VF16, VF17w   	NOP
        MULAx.xyzw  ACC,  VF21, VF18x   	NOP
        MADDAy.xyzw ACC,  VF22, VF18y   	NOP
        MADDAz.xyzw ACC,  VF23, VF18z   	NOP
        MADDw.xyzw  VF26, VF24, VF18w   	LQ VF17, 0(VI08)
        NOP 					IADDIU VI08, VI08, 4
	OPMULA.xyz ACCxyz, VF25xyz, VF26xyz	IADDIU VI06, VI00, 0	
	OPMSUB.xyz VF24xyz, VF26xyz, VF25xyz	LQI VF28, (VI06++)
        MULAx.xyzw  ACC,  VF01, VF17x		LQI VF29, (VI06++)
        MADDAy.xyzw ACC,  VF02, VF17y		LQI VF30, (VI06++)
        MADDAz.xyzw ACC,  VF03, VF17z 		LQI VF31, (VI06++)
	MADDw.xyzw  VF19, VF04, VF17w		ERSADD P, VF24
	MULAx.xyzw  ACC, VF05, VF17x 		LQI VF26, (VI08++)
	MADDAy.xyzw ACC,  VF06, VF17y		NOP
	MADDAz.xyzw ACC,  VF07, VF17z		NOP
	MADDw.xyzw  VF20, VF08, VF17w		NOP
	MULAx.xyzw  ACC,  VF09, VF17x		NOP 
	MADDAy.xyzw ACC,  VF10, VF17y		NOP 
	MADDAz.xyzw ACC,  VF11, VF17z		NOP
	MADDw.xyzw  VF21, VF12, VF17w		NOP
	MULAx.xyzw  ACC,  VF13, VF17x		NOP 
	MADDAy.xyzw ACC,  VF14, VF17y		NOP 
	MADDAz.xyzw ACC,  VF15, VF17z		NOP
	MADDw.xyzw  VF22, VF16, VF17w		NOP
        MULAx.xyzw  ACC,  VF19, VF18x   	NOP
        MADDAy.xyzw ACC,  VF20, VF18y   	NOP
        MADDAz.xyzw ACC,  VF21, VF18z   	WAITP
        MADDw.xyzw  VF23, VF22, VF18w   	MFP.x VF25, P
	MULAz.x ACC, VF26x, VF27z		LQ VF17, 0(VI08)
	MULAw.y ACC, VF26y, VF27w 		NOP
	MADDw.xy VF26xy, VF27xy, VF00w 		NOP               
	MULAx.xyzw  ACC,  VF28, VF23x		ESQRT P, VF25x
	MADDAy.xyzw ACC,  VF29, VF23y 		LQ VF18, 3(VI08)
	MADDAz.xyzw ACC,  VF30, VF23z 		IADDIU VI06, VI00, 4
        MADDw.xyzw  VF19, VF31, VF23w 		SQ VF26, 0(VI09)
	MULAx.xyzw  ACC,  VF01, VF17x 		IADDIU VI09, VI09, 3
	MADDAy.xyzw ACC,  VF02, VF17y 		LQI VF28, (VI06++)
	MADDAz.xyzw ACC,  VF03, VF17z 		LQI VF29, (VI06++)
	MADDw.xyzw  VF21, VF04, VF17w		DIV Q, VF00w, VF19w
	MULAx.xyzw  ACC,  VF05, VF17x 		LQI VF30, (VI06++)
	MADDAy.xyzw ACC,  VF06, VF17y 		LQI VF31, (VI06++)
	MADDAz.xyzw ACC,  VF07, VF17z		WAITP
	MADDw.xyzw  VF22, VF08, VF17w		MFP.x VF25x, P
	MULAx.xyzw  ACC,  VF09, VF17x 		NOP
	MADDAy.xyzw ACC,  VF10, VF17y 		NOP
	MULq VF20, VF19, Q 			NOP
	MULx.xyz VF26xyz, VF24xyz, VF25x 	NOP
	MADDAz.xyzw ACC,  VF11, VF17z 		NOP
	MADDw.xyzw  VF23, VF12, VF17w		NOP
	FTOI4 VF19, VF20 			NOP
	MULAx.xyz ACC, VF29, VF26x		NOP
	MADDAy.xyz ACC, VF30, VF26y		NOP
	MADDz.xyz VF25, VF31, VF26z		NOP
	MULAx.xyzw  ACC,  VF13, VF17x		SQ.xyzw VF19, 0(VI05)
	MADDAy.xyzw ACC,  VF14, VF17y		IADDIU VI05, VI05, 3
	MADDAz.xyzw ACC,  VF15, VF17z 		IADDIU VI01, VI01, 1
	MADDw.xyzw  VF24, VF16, VF17w		NOP
	MULAx.xyz ACC, VF28xyz, VF25x		LQI VF30, (VI06++) ; load MAX
	MADDAy.xyz ACC, VF28xyz, VF25y		NOP               
	MADDz.xyz VF29xyz, VF28xyz, VF25z	LQI VF31, (VI06++) ; load MIN
	MULAx.xyzw  ACC,  VF21, VF18x		LQ VF17, 1(VI08)
	MADDAy.xyzw ACC,  VF22, VF18y		NOP               
	MADDAz.xyzw ACC,  VF23, VF18z  		NOP
	MAX.xyzw VF29, VF29, VF30		NOP 
	MADDw.xyzw  VF25, VF24, VF18w  		NOP
	MULAx.xyzw  ACC,  VF01, VF17x		NOP 
	MADDAy.xyzw ACC,  VF02, VF17y		NOP 
	MINI.xyzw VF30, VF29, VF31		NOP
	MADDAz.xyzw ACC,  VF03, VF17z		NOP
        MADDw.xyzw  VF21, VF04, VF17w		NOP
        MULAx.xyzw  ACC,  VF05, VF17x		NOP
	FTOI0 VF19, VF30			NOP
        MADDAy.xyzw ACC,  VF06, VF17y		NOP
        MADDAz.xyzw ACC,  VF07, VF17z		NOP
        MADDw.xyzw  VF22, VF08, VF17w		NOP
        MULAx.xyzw  ACC,  VF09, VF17x   	SQ VF19, 0(VI04)
        MADDAy.xyzw ACC,  VF10, VF17y   	IADDIU VI04, VI04, 3
        MADDAz.xyzw ACC,  VF11, VF17z   	IBNE VI01, VI03, LOOP01
	NOP 	NOP

	;------------------------------------------------------------
	NOP	ISUBIU VI02, VI02, 1
	ADDz.x VF27x, VF27x, VF27z	NOP
	NOP	IBGTZ VI02, LOOP02
	NOP 	NOP

	NOP	IADDIU VI06, VI07, 38 ; --- set first XYZ ---
        NOP     IADDIU VI02, VI11, 0
        NOP     IADDIU VI03, VI00, 0x7fff ; can not add 0x8000 since imm is only 15 bits
        NOP     IADDIU VI03, VI03, 1  ; to create 0x8000(this is neccesary).
LOOP3:
        NOP     ISW.w VI03,  0(VI06)
        NOP     ISW.w VI03,  3(VI06)
        NOP     ISW.w VI03, 24(VI06)
        NOP     ISW.w VI03, 27(VI06)
        NOP     ISW.w VI03, 48(VI06)
        NOP     ISW.w VI03, 51(VI06)
        NOP     IADDIU VI06, VI06, 72
        NOP     ISUBIU VI02, VI02, 1
        NOP     NOP
        NOP     IBGTZ VI02, LOOP3
        NOP 	NOP
	NOP 	NOP
	;------------------------------------------------------------
	NOP	IADDIU VI06, VI07, 35 ; --- set XGKICK point ---
        NOP     XGKICK VI06
        NOP[E]  NOP
        NOP     NOP
        NOP     B START0
        NOP     NOP
	



