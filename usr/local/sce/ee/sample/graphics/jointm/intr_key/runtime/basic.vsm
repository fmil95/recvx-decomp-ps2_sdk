/* SCEI CONFIDENTIAL
 "PlayStation 2" Programmer Tool Runtime Library  Release 2.0
 */
/*
 *              Emotion Engine Library Sample Program
 *
 *
 *                         Version 0.5.0
 *                           Shift-JIS
 *
 *      Copyright (C) 1998-1999 Sony Computer Entertainment Inc.
 *                        All Rights Reserved.
 *
 *                     Name : joint matrix interpolation runtime
 *                     Description : vu1 micro codes
 *
 *       Version        Date            Design      Log
 *  --------------------------------------------------------------------
 *       0.5.0          Aug,16,1999     aoki
 */

; ---------------------------------------------------------------------
;
;
;       vu1 micro code for basic sample
;
;
;	VF01 - VF04 	: perspective + transformation matrix 
;	VF05 - VF08 	: light color matrix
;       VF09 - VF12     :  X light vector matrix(3 lighting vector + 1 ambient)
;	VF15        	: gif tag 
;	VF20 		: source vertex
;	VF21 		: source normal
;	VF22 		: source st
;	VF23 		: source color
;
;
;	VI02 : pointer for gif tag / for XGKICK
;	VI03 : pointer for vertex
;	VI04 : pointer for normal
;	VI05 : pointer for st
;	VI06 : pointer for color
;	VI07 : store pointer for RGBAQ
;	VI08 : store pointer for ST
;	VI09 : store pointer for XYZF2
;
;       VI10 : counter for vertices
;
;
; ---------------------------------------------------------------------
	NOP	IADDIU VI01, VI00, 0
	NOP	IADDIU VI02, VI00, 4
	NOP	IADDIU VI03, VI00, 16	; screen matrix X object matrix
	NOP	BAL VI15, MulMatrix
	NOP	NOP

	NOP	IADDIU VI01, VI00, 8 
	NOP	LQI VF09, (VI01++)	; (8) load light color matrix 
	NOP	LQI VF10, (VI01++)
	NOP	LQI VF11, (VI01++)
	NOP	LQI VF12, (VI01++)
	NOP	LQI VF05, (VI01++)	; (12) load light vector matrix 
	NOP	LQI VF06, (VI01++)
	NOP	LQI VF07, (VI01++)
	NOP	LQI VF08, (VI01++)
	NOP	LQI VF01, (VI01++)	; (16) load screen x transformation matrix
	NOP	LQI VF02, (VI01++)
	NOP	LQI VF03, (VI01++)
	NOP	LQI VF04, (VI01++)

	NOP[E]	NOP
	NOP	NOP

START0:
	NOP 	XTOP VI01
	NOP	ILWR.x VI10, (VI01)x 		; copy to VI10
	NOP	IADDIU VI11, VI00, 0x7fff	; set mask
	NOP	IAND VI10, VI11, VI10		; mask. VI10 is counter for vertices.
	NOP 	LQ VF19, 0(VI01) 		; load gif tag
	NOP	IADDIU VI03, VI01, 1		; pointer for vertices
	NOP	IADD VI04, VI03, VI10		; pointer for normal
	NOP	IADD VI05, VI04, VI10		; pointer for color
;	NOP	IADD VI06, VI05, VI10		; pointer for st
	NOP	NOP
;	NOP	IADD VI02, VI06, VI10		; set XGKICK pointer
	NOP	IADD VI02, VI05, VI10		; set XGKICK pointer
	NOP 	SQ VF19, 0(VI02) 		; store gif tag
	NOP	IADDIU VI07, VI02, 1		; set store pointer for RGBAQ
;	NOP	IADDIU VI08, VI02, 1		; set store pointer for ST
	NOP	IADDIU VI09, VI02, 2		; set store pointer for XYZF2

LOOP:
	NOP	LQI VF20, (VI03++)		; load vertex
	NOP	LQI VF21, (VI04++)		; load normal
	NOP	LQI VF22, (VI05++)		; load color
;	NOP	LQI VF23, (VI06++)		; load st
	NOP	NOP

	; --- rotation/translation/perspective transform ---
	MULAx.xyzw ACC, VF01, VF20x	NOP ; rot/trans/pers 	 
	MADDAy.xyzw ACC, VF02, VF20y	NOP	 
	MADDAz.xyzw ACC, VF03, VF20z	NOP	 
	MADDw.xyzw VF25, VF04, VF20w	NOP	 
	NOP NOP
	NOP NOP
	NOP NOP
	NOP				DIV Q, VF00w, VF25w

	; --- light matrix X normal ---
	MULAx.xyzw ACC, VF05, VF21x	NOP ; dot products	 
	MADDAy.xyzw ACC, VF06, VF21y	NOP	 
	MADDAz.xyzw ACC, VF07, VF21z	NOP	 
	MADDw.xyzw VF13, VF08, VF21w	NOP	 
	NOP NOP
	NOP NOP
	NOP NOP
	MAXx.xyzw VF13, VF13, VF00x	NOP ; cut under 0.0f

	; --- calc transformed position and store it ---
	MULq VF28, VF25, Q		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	FTOI4 VF27, VF28		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	NOP 				SQ VF27, 0(VI09) ; --- store XYZF2 ---
;	NOP				IADDIU VI09, VI09, 3
	NOP				IADDIU VI09, VI09, 2

	; --- load st and store it ---
;	MULq.xyz VF27, VF23, Q		NOP
;	NOP NOP
;	NOP NOP
;	NOP NOP
;	NOP SQ VF27, 0(VI08)
;	NOP IADDIU VI08, VI08, 3
;	NOP NOP
;	NOP NOP

	; --- color * dot-product ---
	MULAx.xyzw ACC, VF09, VF13x	NOP
	MADDAy.xyzw ACC, VF10, VF13y	NOP
	MADDAz.xyzw ACC, VF11, VF13z	NOP
	MADDw.xyzw VF14, VF12, VF13w	NOP
	NOP NOP
	NOP NOP
	NOP NOP
	MUL.xyz VF27, VF22, VF14	NOP
	NOP NOP
	NOP NOP
	NOP LOI 255.0
	MINIi VF27, VF27, I		NOP
	NOP NOP
	NOP NOP
	NOP LOI 0
	MAXi VF27, VF27, I		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	FTOI0 VF26, VF27		NOP
	NOP NOP
	NOP NOP
	NOP NOP
	NOP 				SQ VF26, 0(VI07)
;	NOP				IADDIU VI07, VI07, 3
	NOP				IADDIU VI07, VI07, 2
	
	; --- decrement loop ---
	NOP	IADDI VI10, VI10, -1	
	NOP	IBNE VI10, VI00, LOOP

	; --- send result to GIF and stop ---
	NOP  	NOP   
	NOP   	XGKICK VI02  
	NOP[E]  NOP   
	NOP	NOP
	NOP   	B START0  
	NOP	NOP      ;Take care!! Don't forget.


; -------------------------------------------------------------------
;   MulMatrix 
;      VI01 : address of source matrix
;      VI02 : address of source matrix
;      VI03 : address of destination matrix
;

MulMatrix:
	NOP                          	LQI.xyzw VF08, (VI02++)
        NOP                            	LQI.xyzw VF04, (VI01++)
        NOP                            	LQI.xyzw VF05, (VI01++)
        NOP                            	LQI.xyzw VF06, (VI01++)
        NOP                            	LQI.xyzw VF07, (VI01++)
        MULAx.xyzw ACC, VF04, VF08x     LQI.xyzw VF09, (VI02++)
        MADDAy.xyzw ACC, VF05, VF08y    NOP
        MADDAz.xyzw ACC, VF06, VF08z    NOP
        MADDw.xyzw VF12, VF07, VF08w    NOP
        MULAx.xyzw ACC, VF04, VF09x     LQI.xyzw VF10, (VI02++)
        MADDAy.xyzw ACC, VF05, VF09y    NOP
        MADDAz.xyzw ACC, VF06, VF09z    NOP
        MADDw.xyzw VF13, VF07, VF09w    SQI.xyzw VF12, (VI03++)
        MULAx.xyzw ACC, VF04, VF10x     LQI.xyzw VF11, (VI02++)
        MADDAy.xyzw ACC, VF05, VF10y    NOP
        MADDAz.xyzw ACC, VF06, VF10z    NOP
        MADDw.xyzw VF14, VF07, VF10w    SQI.xyzw VF13, (VI03++)
        MULAx.xyzw ACC, VF04, VF11x     NOP
        MADDAy.xyzw ACC, VF05, VF11y    NOP
        MADDAz.xyzw ACC, VF06, VF11z    NOP
        MADDw.xyzw VF15, VF07, VF11w    SQI.xyzw VF14, (VI03++)
        NOP                            	NOP
        NOP                            	NOP
        NOP                            	NOP
        NOP                            	SQI.xyzw VF15, (VI03++)
        NOP                            	NOP
        NOP                            	JR VI15
        NOP                            	NOP
	












